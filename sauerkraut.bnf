/*
 * Este archivo se complementa con 'sauerkraut.l',
 * que contiene los tokens aqu√≠ utilizados.
 */


<S>               ::= <ST>
<ST>              ::= <EXPR> <DELIM> <ST> | <EXTERN_FUNC> <DELIM> <ST> | <FUNC> <DELIM> <ST> | <VAR> <DELIM> <ST> |
                      <IF> <DELIM> <ST> | <WHILE> <DELIM> <ST> | <RET> <DELIM> | <DELIM> <ST> | /*empty*/
<DELIM>           ::= ';' | '\n' 
<RET>             ::= <RETKW> <I> 
<EXPR>            ::= <INSTR> 
<VAR>             ::= <VARKW> <IDENT> | <VARKW> <IDENT> '=' I
<FUNC>            ::= <FUNKW> <IDENT> <ARGS> '{' <ST> '}' 
<OBJECT>          ::= '{' <KV_SET> '}' 
<KV_SET>          ::= <STR> ':' <I> <DELIM> <KV_SET> | <DELIM> <KV_SET> | /*empty*/ 
<ARRAY>           ::= '[' <V_SET> ']' | '[' ']' 
<V_SET>           ::= <I> ',' <V_SET> | <I>
<ARGS>            ::= '(' <ARGSET> ')' | '(' ')'
<ARGSET>          ::= <IDENT> | <IDENT> ',' <ARGSET>
<IDENT>           ::= <ID>
<EXTERN_FUNC>     ::= <EXTERNKW> <FUNKW> <IDENT> '(' <EXTERN_FUNC_ARGS> ')' | <EXTERNKW> <FUNKW> <IDENT> '(' ')'
<EXTERN_FUNC_ARGS>::= <ID> | <ID> ',' <EXTERN_FUNC_ARGS>
<INT>             ::= <INTEGER>
<STR>             ::= <STRING>
<INSTR>           ::= <I> | <ASSIGN>
<CALL>            ::= <IDENT> '(' <PASSEDARGSPREV> ')' | <I> '.' <IDENT> '(' <PASSEDARGSPREV> ')'
<PASSEDARGSPREV>  ::= <PASSEDARGS> | /*empty*/
<PASSEDARGS>      ::= <I> ',' <PASSEDARGS> | <I>
<WHILE>           ::= <WHILEKW> '(' <INSTR> ')' '{' <ST> '}'
<IF>              ::= <IFKW> '(' <INSTR> ')' '{' <ST> '}' <ELSEKW> '{' <ST> '}' |
                      <IFKW> '(' <INSTR> ')' '{' <ST> '}' 
<ASSIGN>          ::= <IDENT> '=' <I>
<I>               ::= <I> '+' <I> | '(' <I> ')' | <CALL> | <I> '-' <I> | <I> '*' <I> | <I> '/' <I> | <I> '%' <I> |
                      <I> '<' <I> | <I> '>' <I> | <I> <LE> <I> | <I> <GE> <I> | <I> <EQ> <I> | <I> <NE> <I> |
                      <I> <AND> <I> | <I> <OR> <I> | '!' <I> | '-' <I> | <I> '[' <I> ']' |  <I> '[' <I> ']' '=' <I> |
                      <IDENT> | <VALUE>
<VALUE>           ::= <INT> | <STR> | <ARRAY> | <OBJECT>
